plugins {
    id "com.github.node-gradle.node" version "2.2.3"
}

node {
    version = '18.12.1'
    download = true
}


task ngBuild(type: NpxTask) {
    dependsOn npmInstall
    command = 'ng'
    args = project.hasProperty('production') && project.ext.production.toBoolean() ? ['build', '-c', 'production'] : ['build']
    inputs.files('package.json', 'package-lock.json', 'angular.json', 'tsconfig.json')
    inputs.dir('src')
    inputs.dir(fileTree("node_modules").exclude(".cache"))
    outputs.dir('dist')
}

task ngServe(type: NpxTask) {
    dependsOn npmInstall
    command = 'ng'
    args = ['serve']
    inputs.files('package.json', 'package-lock.json', 'angular.json', 'tsconfig.json')
    inputs.dir('src')
    inputs.dir(fileTree("node_modules").exclude(".cache"))
    outputs.dir('dist')
}

npmInstall.inputs.files('package.json')
npmInstall.outputs.dir('node_modules')

task clean(type: Delete) {
    group = 'build'
    delete 'dist'
}

task buildDist(type: Zip, dependsOn: [ngBuild]) {
    group = 'build'
    archiveFileName = "${project.name}.zip"
    destinationDirectory = layout.buildDirectory.dir('libs')
    from layout.projectDirectory.dir('dist')
    include '*.css'
    include '*.html'
    include '*.js'
}

task assemble (dependsOn: [buildDist]) {
    group = 'build'
}

task build(dependsOn: [assemble]) {
    group = 'build'
}